set windows-shell := ["pwsh.exe", "-c"]

@default: build

[windows]
build:
    pulumi plugin rm language gestalt -y
    go build
    pulumi plugin install language gestalt 1.0 --file ./pulumi-language-gestalt.exe

[linux]
[macos]
build:
    pulumi plugin rm language gestalt -y
    go build
    pulumi plugin install language gestalt 1.0 --file ./pulumi-language-gestalt

fmt:
    go fmt .

build-language-plugin $GOOS $GOARCH:
    go build

package-language-plugin GOOS GOARCH VERSION:
	just build-language-plugin {{GOOS}} {{GOARCH}}
	tar czvf pulumi-language-gestalt-v{{VERSION}}-{{GOOS}}-{{GOARCH}}.tar.gz pulumi-language-gestalt
	rm pulumi-language-gestalt

package-language-plugin-windows GOOS GOARCH VERSION:
	just build-language-plugin {{GOOS}} {{GOARCH}}
	tar czvf pulumi-language-gestalt-v{{VERSION}}-{{GOOS}}-{{GOARCH}}.tar.gz pulumi-language-gestalt.exe
	rm pulumi-language-gestalt.exe

package-language-plugin-all VERSION:
	just package-language-plugin darwin arm64 {{VERSION}}
	just package-language-plugin darwin amd64 {{VERSION}}
	just package-language-plugin linux arm64 {{VERSION}}
	just package-language-plugin linux amd64 {{VERSION}}
	just package-language-plugin-windows windows arm64 {{VERSION}}
	just package-language-plugin-windows windows amd64 {{VERSION}}